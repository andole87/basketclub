plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.moowork.node' version '1.3.1'
    id 'org.asciidoctor.convert' version '1.5.9.2'
    id 'java'
}

group = 'com.basketclub'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

ext {
    acceptanceDir = 'src/acceptance'
    set('snippetsDir', file("build/generated-snippets"))
}

configurations {
    acceptanceImplementation.extendsFrom(implementation, testImplementation, runtimeOnly)
}

sourceSets {
    acceptance {
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
        java.srcDir file("${acceptanceDir}/java")
    }
}

task acceptanceTest(type: Test) {
    useJUnitPlatform()
    description = 'run acceptance test'
    testClassesDirs = sourceSets.acceptance.output.classesDirs
    classpath = sourceSets.acceptance.runtimeClasspath
    outputs.dir snippetsDir

    dependsOn test
}

check {
    dependsOn acceptanceTest
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:3.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'net.rakugakibox.spring.boot:logback-access-spring-boot-starter:2.7.1'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    runtimeOnly 'com.h2database:h2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    useJUnitPlatform()
}
